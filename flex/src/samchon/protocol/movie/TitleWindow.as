package samchon.protocol.movie
{
	import flash.events.MouseEvent;
	
	import mx.events.FlexEvent;
	import mx.managers.PopUpManager;
	
	import samchon.library.ui.TitleWindow;
	import samchon.protocol.invoke.IProtocol;
	import samchon.protocol.invoke.Invoke;
	
	/**
	 * TitleWindow is generated by Window having IProtocol<br/>
	 * Although a TitleWindow can be made by Movie, make it in Window<br/>
	 * 
	 * @copy org.samchon.library.ui.TitleWindow 
	 */ 
	public class TitleWindow 
		extends samchon.library.ui.TitleWindow 
		implements IProtocol
	{
		/**
		 * Parent Window who made this TitleWindow
		 */
		protected var window:Window;
		/**
		 * Pointer of Movie from Window
		 */
		protected function get movie():Movie
		{
			return window.getMovie();
		}
		
		/**
		 * Whether the creation was completed or not
		 */
		protected var creationFlag:Boolean = false;
		
		/**
		 * Constructor.<br/>
		 * <br/>
		 * @param window Parent Window who made this TitleWindow
		 */
		public function TitleWindow(window:Window = null)
		{
			super();
			
			this.window = window;
			this.addEventListener(FlexEvent.CREATION_COMPLETE, creationCompleted);
		}
		/**
		 * Handler of Creation Complete<br/>
		 * If you want to do something more, override this method
		 */
		protected function creationCompleted(event:FlexEvent):void
		{
			this.removeEventListener(FlexEvent.CREATION_COMPLETE, creationCompleted);
			creationFlag = true;
		}
		
		//SOCKET
		public function replyData(invoke:Invoke):void
		{
			
		}
		public function sendData(invoke:Invoke):void
		{
			window.sendData(invoke);
		}
		
		/**
		 * Static factory method for TitleWindow<br/>
		 * Do not recommend to create TitleWindow by this. Use each createPopUp method in Window and Movie<br/>
		 * <br/>
		 * 
		 * @param window Parent Window
		 * @param $class Target TitleWindow class to create
		 * 
		 * @see Window
		 * @see Movie
		 */
		public static function createPopUp(window:Window, $class:Class):samchon.protocol.movie.TitleWindow
		{
			var popUp:samchon.protocol.movie.TitleWindow = 
				PopUpManager.createPopUp(window, $class, true) as samchon.protocol.movie.TitleWindow;
			popUp.window = window;
			PopUpManager.centerPopUp(popUp);
			
			return popUp;
		}
	}
}